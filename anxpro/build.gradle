apply plugin : 'application'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

dependencies {
    compile project(':core')
    compile "io.socket:socket.io-client:1.0.0"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "com.google.code.gson:gson:2.8.2"
    compile 'org.apache.kafka:kafka-clients:1.0.0'

    testCompile "info.cukes:cucumber-java8:1.2.5"
    testCompile "info.cukes:cucumber-junit:1.2.5"
}

mainClassName = 'com.blokaly.ceres.anx.AnxApp'
applicationName = "anxfh"
version = 1.0

// conf files should be under src/conf folder, avoid duplication
sourceSets {
    main {
        resources {
            exclude '**/*'
        }
    }
}

applicationDistribution.from("src/conf") { into "conf" }

applicationDefaultJvmArgs = [
        "-Dlog.root=MY_APP_HOME/logs",
        "-Dlog.name=${applicationName}",
        "-server", "-showversion", "-Xms512m", "-Xmx512m",
        "-XX:-OmitStackTraceInFastThrow",
        "-XX:+HeapDumpOnOutOfMemoryError",
        "-XX:HeapDumpPath=MY_APP_HOME/logs/gc.hprof",
        "-XX:+DisableExplicitGC",
        "-XX:+PrintGCDetails",
        "-XX:+PrintGCApplicationStoppedTime",
        "-XX:+PrintGCApplicationConcurrentTime",
        "-XX:+PrintGCDateStamps",
        "-XX:+UseConcMarkSweepGC",
        "-XX:+ScavengeBeforeFullGC",
        "-XX:CMSInitiatingOccupancyFraction=55",
        "-XX:+CMSScavengeBeforeRemark",
        "-XX:-CMSIncrementalMode",
        "-XX:-CMSClassUnloadingEnabled",
        "-XX:+CMSConcurrentMTEnabled",
        "-XX:+CMSParallelRemarkEnabled",
        "-XX:+UseStringCache",
        "-XX:+OptimizeStringConcat",
        "-Xloggc:MY_APP_HOME/logs/gc.log"
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
        unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
        windowsScript.text = windowsScript.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
    }
}

